plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace "sdk.chat.ui.extras"
    compileSdk Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.MIN_SDK)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    buildTypes {
        debug {
            minifyEnabled project.hasProperty('minifyDebug')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled project.hasProperty('minifyCore')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        lintConfig file('lint.xml')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':chat-sdk-core-ui')

    //required support lib modules
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerViewVersion"
    implementation "androidx.annotation:annotation:$androidxAnnotation"

    // Add for NavController support
    api "com.mikepenz:materialdrawer-nav:$materialDrawerVersion"

    // Need this: maven { url "https://dl.bintray.com/mikepenz/maven" }
    api "com.mikepenz:materialdrawer-iconics:$materialDrawerVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "me.dm7.barcodescanner:zxing:$zxing"
}

ext {
    libraryName = 'UIExtrasMod'
    artifact = 'mod-ui-extras'
    libraryDescription = 'UI Extras Module'
}

apply from: '../bintray.gradle'
